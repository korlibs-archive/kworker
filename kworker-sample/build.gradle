dependencies {
    commonMainApi rootProject
}

kotlin.targets.macosX64.compilations.main.outputKinds("EXECUTABLE")
kotlin.targets.mingwX64.compilations.main.outputKinds("EXECUTABLE")
kotlin.targets.linuxX64.compilations.main.outputKinds("EXECUTABLE")

afterEvaluate {
    task runMacosX64(type: Exec, dependsOn: [linkDebugExecutableMacosX64]) {
        workingDir linkDebugExecutableMacosX64.outputFile.get().parentFile
        commandLine linkDebugExecutableMacosX64.outputFile.get()
    }

    task runMingwX64(type: Exec, dependsOn: [linkDebugExecutableMingwX64]) {
        workingDir linkDebugExecutableMingwX64.outputFile.get().parentFile
        commandLine linkDebugExecutableMingwX64.outputFile.get()
    }

    task runLinuxX64(type: Exec, dependsOn: [linkDebugExecutableLinuxX64]) {
        workingDir linkDebugExecutableLinuxX64.outputFile.get().parentFile
        commandLine linkDebugExecutableLinuxX64.outputFile.get()
    }

    //println(kotlin.targets.js.compilations.test.output)
    task runJs(type: NodeTask, dependsOn: [kotlin.targets.js.compilations.test.compileKotlinTaskName, installMocha, populateNodeModules]) {
        script = new File(buildDir, "node_modules/${project.name}.js")
    }

    task runJvm(type: JavaExec, dependsOn: [jvmTestClasses]) {
        classpath = kotlin.targets.jvm.compilations.test.runtimeDependencyFiles
        main = "SampleKt"
    }
}

afterEvaluate {
    task jsWeb(type: Copy, dependsOn: []) {
        from kotlin.targets.js.compilations.main.output.allOutputs
        kotlin.targets.js.compilations.test.runtimeDependencyFiles.each {
            if (it.exists()) {
                if (!it.isDirectory()) {
                    from zipTree(it.absolutePath)
                } else {
                    from it
                }
            }
        }
        kotlin.targets.js.compilations.main.kotlinSourceSets.resources.collectMany { it.srcDirs }.each {
            from it
        }
        into "$buildDir/web"

    }
}

def checkOutput(String output) {
    def expectedOutput = """Sending messages
IN WORKER 1 WorkerMessage[hello](world)
IN WORKER 1 WorkerMessage[hello](world)
IN WORKER 1 WorkerMessage[hello](world)
IN MAIN 0 WorkerMessage[reply](demo)
IN MAIN 0 WorkerMessage[reply](demo)
IN MAIN 0 WorkerMessage[reply](demo)
IN WORKER 2 WorkerMessage[hello](world)
IN WORKER 2 WorkerMessage[hello](world)
IN MAIN 0 WorkerMessage[reply](demo)
IN MAIN 0 WorkerMessage[reply](demo)
"""
    assert(output.toString().readLines().toSorted() == expectedOutput.readLines().toSorted())
}

afterEvaluate {
    task checkIntegrationJvm(type: JavaExec, dependsOn: [jvmTestClasses]) {
        classpath = kotlin.targets.jvm.compilations.test.runtimeDependencyFiles
        main = "SampleKt"
        standardOutput = new ByteArrayOutputStream()
        doLast {
            checkOutput(standardOutput.toString())
        }
    }

    task checkIntegrationNodeJs(type: Exec, dependsOn: [kotlin.targets.js.compilations.test.compileKotlinTaskName, installMocha, populateNodeModules]) {
        commandLine node.variant.nodeExec, new File(buildDir, "node_modules/${project.name}.js")
        standardOutput = new ByteArrayOutputStream()

        doLast {
            checkOutput(standardOutput.toString())
        }
    }

    String nativeLinkTaskName = "linkDebugExecutableMacosX64"

    if (org.apache.tools.ant.taskdefs.condition.Os.isFamily(org.apache.tools.ant.taskdefs.condition.Os.FAMILY_UNIX)) {
        nativeLinkTaskName = "linkDebugExecutableLinuxX64"
    }
    if (org.apache.tools.ant.taskdefs.condition.Os.isFamily(org.apache.tools.ant.taskdefs.condition.Os.FAMILY_WINDOWS)) {
        nativeLinkTaskName = "linkDebugExecutableMingwX64"
    }
    if (org.apache.tools.ant.taskdefs.condition.Os.isFamily(org.apache.tools.ant.taskdefs.condition.Os.FAMILY_MAC)) {
        nativeLinkTaskName = "linkDebugExecutableMacosX64"
    }
    def linkTask = project.tasks.findByName(nativeLinkTaskName)

    task checkIntegrationNative(type: Exec, dependsOn: [linkTask]) {
        workingDir linkTask.outputFile.get().parentFile
        commandLine linkTask.outputFile.get()
        standardOutput = new ByteArrayOutputStream()

        doLast {
            checkOutput(standardOutput.toString())
        }
    }

    tasks.check.finalizedBy checkIntegrationJvm
    tasks.check.finalizedBy checkIntegrationNodeJs
    tasks.check.finalizedBy checkIntegrationNative
}
